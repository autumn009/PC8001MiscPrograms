0 REM˜•••••••••••••••••••••••••••••••™
0 REM–    ˜•••••••••••••••••™        –
0 REM–    –3 Dimentions Maze–        –
0 REM–    š•••••••••••••••••›        –
0 REM–                     ver1.0    –
0 REM–                               –
0 REM–   Programed by                –
0 REM–               Akira Kawamata  –
0 REM–                               –
0 REM–        Apr.19,1980            –
0 REM–                               –
0 REM–Copyright 1980 (C)by A.Kawamata
0 REM–                               –
0 REM–                               –
0 REM–  No part of this program      –
0 REM–     shuld be used             –
0 REM–          without permission.  –
0 REM–                               –
0 REMš•••••••••••••••••••••••••••••••
0 REM
0 REM
0 REM
50 WIDTH80,25:CONSOLE0,25,0,1:COLOR7,0,1:PRINTCHR$(12)
60 CLEAR:   DEFINTA-Z
65 GOSUB 20000
70    MX=22:MY=22
80    DIM M(MX,MY),PT(338),TA(3)
90 X9=79 :Y9=49 :A=2
92 X0=(X9+(X9*0.4))*(A/(A+1))
93 Y0=(Y9+(Y9*0.4))*(A/(A+1))
95 FORI=0TO3:READTA(I):NEXT:DATA-1,1,1,-1
100 GY=INT(RND(1)*(MY-2))+1 : GOSUB10000:MA=INT(RND(1)*4)
1000 I=0:WA=0
1020 IFMA=0ORMA=3THENI=I-1:ELSEI=I+1
1040 IFMA=0ORMA=2THENX=WX:Y=WY+I:ELSEX=WX+I:Y=WY
1060 IFM(X,Y)=0AND(X<>MX)THEN1020:ELSEEN=ABS(I)-1:IFY=GYANDX=MXTHENWA=1:EN=EN+1
1080 ST=TA(MA):WL=0:WR=0
1100 COLOR3,0,1:PRINTCHR$(12):ON MA+1 GOTO3000,3500,2000,2500
2000 COLOR7,0,1:X1=X0:Y1=Y0
2002 IF M(WX-1,WY)=0THENGOSUB5500:ELSEGOSUB5550
2004 IF M(WX+1,WY)=0THENGOSUB5600:ELSEGOSUB5650
2018 IFABS(EN)=0THENGOSUB5400:GOTO2260:ELSEFORI=0TOST*(EN-1)STEPST
2020 X2=X1:X1=X0*(A/(ABS(I)+A))
2040 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
2060 IFABS(I)>9THENCOLOR5,0,1
2080 IFABS(I)>15THENCOLOR3,0,1
2100 IF M(WX-1,WY+I)=0THENGOSUB5000:ELSEGOSUB5040
2120 IF M(WX+1,WY+I)=0THENGOSUB5060:ELSEGOSUB5100
2140 NEXT
2160 X2=X1:X1=X0*(A/(ABS(I)+A))
2180 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
2200 'IF M(WX,WY+ST)=1THENGOSUB 5400:GOTO2260
2220 IF M(WX-1,WY+I)=0THENGOSUB5120:ELSEGOSUB5180
2240 IF M(WX+1,WY+I)=0THENGOSUB5260:ELSEGOSUB5320
2260 GOTO 8000
2500 COLOR7,0,1:X1=X0:Y1=Y0
2502 IF M(WX,WY-1)=0THENGOSUB5500:ELSEGOSUB5550
2504 IF M(WX,WY+1)=0THENGOSUB5600:ELSEGOSUB5650
2518 IFABS(EN)=0THENGOSUB5400:GOTO2760:ELSEFORI=0TOST*(EN-1)STEPST
2520 X2=X1:X1=X0*(A/(ABS(I)+A))
2540 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
2560 IFABS(I)>9THENCOLOR5,0,1
2580 IFABS(I)>15THENCOLOR3,0,1
2600 IF M(WX+I,WY-1)=0THENGOSUB5000:ELSEGOSUB5040
2620 IF M(WX+I,WY+1)=0THENGOSUB5060:ELSEGOSUB5100
2640 NEXT
2660 X2=X1:X1=X0*(A/(ABS(I)+A))
2680 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
2700 IF M(WX+ST,WY)=1THENGOSUB 5400:GOTO2760
2720 IF M(WX+I,WY-1)=0THENGOSUB5120:ELSEGOSUB5180
2740 IF M(WX+I,WY+1)=0THENGOSUB5260:ELSEGOSUB5320
2760 GOTO 8000
3000 COLOR7,0,1:X1=X0:Y1=Y0
3002 IF M(WX+1,WY)=0THENGOSUB5500:ELSEGOSUB5550
3004 IF M(WX-1,WY)=0THENGOSUB5600:ELSEGOSUB5650
3018 IFABS(EN)=0THENGOSUB5400:GOTO3260:ELSEFORI=0TOST*(EN-1)STEPST
3020 X2=X1:X1=X0*(A/(ABS(I)+A))
3040 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
3060 IFABS(I)>9THENCOLOR5,0,1
3080 IFABS(I)>15THENCOLOR3,0,1
3100 IF M(WX+1,WY+I)=0THENGOSUB5000:ELSEGOSUB5040
3120 IF M(WX-1,WY+I)=0THENGOSUB5060:ELSEGOSUB5100
3140 NEXT
3160 X2=X1:X1=X0*(A/(ABS(I)+A))
3180 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
3200 IF M(WX,WY+ST)=1THENGOSUB 5400:GOTO3260
3220 IF M(WX+1,WY+I)=0THENGOSUB5120:ELSEGOSUB5180
3240 IF M(WX-1,WY+I)=0THENGOSUB5260:ELSEGOSUB5320
3260 GOTO 8000
3500 COLOR7,0,1:X1=X0:Y1=Y0
3502 IF M(WX,WY+1)=0THENGOSUB5500:ELSEGOSUB5550
3504 IF M(WX,WY-1)=0THENGOSUB5600:ELSEGOSUB5650
3518 IFABS(EN)=0ANDWA=0THENGOSUB5400:GOTO3760:ELSEFORI=0TOST*(EN-1)STEPST
3520 X2=X1:X1=X0*(A/(ABS(I)+A))
3540 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
3560 IFABS(I)>9THENCOLOR5,0,1
3580 IFABS(I)>15THENCOLOR3,0,1
3600 IF M(WX+I,WY+1)=0THENGOSUB5000:ELSEGOSUB5040
3620 IF M(WX+I,WY-1)=0THENGOSUB5060:ELSEGOSUB5100
3640 NEXT
3660 X2=X1:X1=X0*(A/(ABS(I)+A))
3680 Y2=Y1:Y1=Y0*(A/(ABS(I)+A))
3700 IF M(WX+ST,WY)=1THENGOSUB 5400:GOTO3760
3720 IF M(WX+I,WY+1)=0THENGOSUB5120:ELSEGOSUB5180
3740 IF M(WX+I,WY-1)=0THENGOSUB5260:ELSEGOSUB5320
3760 GOTO 8000
5000 LINE(X2+80,50-Y2)-(X2+80,50+Y2),PSET:LINE(X1+80,50-Y1)-(X2+80,50+Y1),PSET,B
5020 RETURN
5040 LINE(X1+80,50-Y1)-(X2+80,50-Y2),PSET:  LINE(X1+80,50+Y1)-(X2+80,50+Y2),  PSET :RETURN
5060 LINE(80-X2,50-Y2)-(80-X2,50+Y2),PSET:LINE(80-X1,50-Y1)-(80-X2,50+Y1),PSET,B
5080 RETURN
5100 LINE(80-X1,50-Y1)-(80-X2,50-Y2),PSET:  LINE(80-X1,50+Y1)-(80-X2,50+Y2),PSET:RETURN
5120 LINE(X2+80,50-Y2)-(X2+80,50+Y2),PSET
5140 LINE(X2+80,50-Y1)-(80,50-Y1),PSET:LINE(X2+80,50+Y1)-(80,50+Y1),PSET
5160 RETURN
5180 LINE(X1+80,50-Y1)-(X2+80,50-Y2),PSET:LINE(X1+80,50+Y1)-(X2+80,50+Y2),PSET
5200 LINE(X1+80,50-Y1)-(X1+80,50+Y1),PSET
5220 IFWA=0THENLINE(X1+80,50-Y1)-(80,50-Y1),PSET:LINE(X1+80,50+Y1)-(80,50+Y1),PSET
5240 RETURN
5260 LINE(80-X2,50-Y2)-(80-X2,50+Y2),PSET
5280 LINE(80-X2,50-Y1)-(79,50-Y1),PSET:LINE(80-X2,50+Y1)-(79,50+Y1),PSET
5300 RETURN
5320 LINE(80-X1,50-Y1)-(80-X2,50-Y2),PSET:LINE(80-X1,50+Y1)-(80-X2,50+Y2),PSET
5340 LINE(80-X1,50-Y1)-(80-X1,50+Y1),PSET
5360 IFWA=0THENLINE(80-X1,50-Y1)-(80,50-Y1),PSET:LINE(80-X1,50+Y1)-(80,50+Y1),PSET
5380 RETURN
5400 LINE(X0+80,50-Y0)-(80-X0,50-Y0),PSET:LINE(X0+80,50+Y0)-(80-X0,50+Y0),PSET
5420 IFWR=1THENLINE(X0+80,50-Y0)-(X0+80,50+Y0),PSET
5430 IFWL=1THENLINE(80-X0,50-Y0)-(80-X0,50+Y0),PSET
5450 RETURN
5500 LINE(X0+80,50-Y0)-(X9+80,50-Y0),PSET:LINE(X0+80,50+Y0)-(X9+80,50+Y0),PSET:RETURN
5550 WR=1 :LINE(X0+80,50-Y0)-(X9+80,50-Y9+1),PSET:LINE(X0+80,50+Y0)-(X9+80,50+Y9-1),PSET:RETURN
5600 LINE(80-X0,50-Y0)-(80-X9,50-Y0),PSET:LINE(80-X0,50+Y0)-(80-X9,50+Y0),PSET:RETURN
5650 WL=1 :LINE(80-X0,50-Y0)-(80-X9,50-Y9),PSET:LINE(80-X0,50+Y0)-(80-X9,50+Y9),PSET:RETURN
8000 IF WA=1 THENGOTO15000
8010 I$=INPUT$(1)
8015 BEEP1:FORI=0TO300:NEXT:BEEP0
8020 IFI$=","THENI=-1:ONMA+1GOSUB8260,8280,8220,8240
8040 IFI$="5"THENI=1:ONMA+1GOSUB8260,8280,8220,8240
8060 IFI$="1"THENMA=MA-1:IFMA=-1THENMA=3
8080 IFI$="3"THENMA=(MA+1)MOD4
8100 IFI$="*"THENGOSUB12000:GOTO8000
8120 IF(M(WX,WY)=0)AND(WX<>MX)THEN8200
8140 IFI$=","THENI$="5":GOTO8180
8160 IFI$="5"THENI$=","
8180 BEEP:GOTO8020
8200 GOTO1000
8220 WY=WY+I:RETURN
8240 WX=WX+(-I):RETURN
8260 WY=WY+(-I):RETURN
8280 WX=WX+I:RETURN
10000 OF=MX*MY/25
10010 LOCATE20,24:PRINT"I'm making maze.  Please wait.  ";
10020 FORX=0TOMX:M(X,0)=1:M(X,MY)=1:NEXT
10040 FORY=0TOMY:M(0,Y)=1:M(MX,Y)=1:NEXT
10060 FORX=2TOMX-2STEP2:FORY=2TOMY-2STEP2:M(X,Y)=1:NEXT:NEXT
10080 FORI=0TOOF
10100 PRINTCHR$(11)I;
10120 X=INT(RND(1)*((MX-2)/2))*2+1
10140 Y=INT(RND(1)*((MY-1)/2))*2
10160 M(X,Y)=1
10180 X=INT(RND(1)*((MX-2)/2))*2+1
10200 Y=INT(RND(1)*((MY-1)/2))*2
10220 M(X,Y)=1
10240 X=INT(RND(1)*((MX-1)/2))*2
10260 Y=INT(RND(1)*((MY-1)/2))*2+1
10280 M(X,Y)=1
10300 X=INT(RND(1)*((MX-1)/2))*2
10320 Y=INT(RND(1)*((MY-1)/2))*2+1
10340 M(X,Y)=1
10360 NEXT
10380 X=INT(RND(1)*(MX-1)/2)*2+1
10400 Y=INT(RND(1)*(MY-1)/2)*2+1
10420 I=0
10440 IFM(X+1,Y)=0THENI=I+1
10460 IFM(X,Y+1)=0THENI=I+1
10480 IFM(X-1,Y)=0THENI=I+1
10500 IFM(X,Y-1)=0THENI=I+1
10520 IF I<3THEN10380
10540 WX=X:WY=Y
10560 M(MX,GY)=0  :RETURN
12000 COLOR6,0,1: FORY=0TOMY:FORX=0TOMX
12020 IF(X=WX)AND(Y=WY)THENPRINTMID$("äåì•æç•ì",MA*2+1,2);:GOTO12060
12040 IFM(X,Y)=1THENPRINT"‡‡";:ELSEPRINT"  ";
12060 NEXT
12080 PRINT
12100 NEXT
12120 RETURN
15000 '
15100 LINE(80-X1+1,50-Y1)-(80+X1-1,50+Y1),PSET,BF
15200 IFWX<>MX-1THENGOTO8010
15300 BEEP:BEEP
15320 LOCATE10,0:PRINT"You get to the exit!";
15340 LOCATE27,1:PRINT"Push any key, then you can play again. ";
15360 I$=INKEY$:IFI$=""THEN15360
15380 GOTO100
20000 COLOR7,0,1:PRINTCHR$(12);
20010 LOCATE10,3:PRINT"*****  3 Dimentions  Maze   *****"
20020 LOCATE15,6:PRINT"  Do you need to explain ?"
20040 LOCATE15,7:PRINT"           Key in Yes or No. "
20050 I$=INKEY$:IFI$=""THEN20050
20060 LOCATE45,7 :IF I$="n"THENPRINT"[ No. ]":GOTO20160 :ELSEPRINT"[ Yes. ]";
20070 LOCATE10,10:PRINT"You will be in Maze."
20080 LOCATE10,11:PRINT"  Then you have to go Exit."
20090 LOCATE10,13:PRINT" If you want to walk, you can use these commands."
20100 LOCATE10,14:PRINT"     [ 5 ]key   walk forword one step "
20110 LOCATE10,15:PRINT"     [ , ]key   walk backword one step "
20120 LOCATE10,16:PRINT"     [ 1 ]key   turn left "
20130 LOCATE10,17:PRINT"     [ 3 ]key   turn right "
20140 LOCATE10,18:PRINT" If you want to watch the maze outside, you can use this command."
20150 LOCATE10,19:PRINT"     [ * ]key   watch Maze "
20160 REM
20170 LOCATE8 ,21:PRINT"Shall we go into the maze ?     [ Push any key, then start. ]   "
20180 I$=INKEY$:IF I$="" THEN20180
20999 RETURN
