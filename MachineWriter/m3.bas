10 REM MACHINE WRITER -3
20 REM  Rev 1.0
30 REM Aug.2,'81
100 WIDTH80,25:CONSOLE0,25,0,1:COLOR7,0,0:PRINT CHR$(12)
110 CLEAR 300,&H97ff
120 COLOR5:PRINT "MACHINE WRITE-3
130 PRINT:PRINT "Enter Command,ser.";:GOSUB2000
140 V$=INPUT$(1):GOSUB2000
150 IF V$="h" THEN 9000
160 IF V$=CHR$(2) THEN END
170 IF V$="m" THEN MON:GOTO130
180 IF V$<=" " THEN 130
190 IF V$="w" THEN 500
195 IF V$="b" THEN 1000
200 IF V$="d" THEN 700
210 PRINT"?":GOTO130
300 REM kay in & write
305 LOCATE J*3+5,I+1:PRINT RIGHT$("0"+HEX$(PEEK(A+I*16+J)),2)" ";
306 LOCATE J*3+5,I+1
310 GOSUB350:V1$=V$
320 IF F THEN 300
330 GOSUB350:IF F THEN 300
340 POKE A+I*16+J,VAL("&h"+V1$+V$):GOSUB2000:COLOR7:PRINT" ";:COLOR5:RETURN
350 V$=INPUT$(1):F=0:GOSUB2000
360 IF V$="," THEN V$="A":GOTO 480
370 IF V$="." THEN V$="B":GOTO 480
380 IF V$=CHR$(13) THEN V$="C":GOTO 480
390 IF V$="=" THEN V$="D":GOTO 480
400 IF V$="+" THEN V$="E":GOTO 480
410 IF V$="*" THEN V$="F":GOTO 480
415 IF V$=>"0" AND V$<="9" THEN 480
416 IF V$=" " THEN I=(I+((J+1)\16))MOD 16:J=(J+1)MOD16
420 IF V$=CHR$(28) THEN J=(J+1)MOD16
430 IF V$=CHR$(29) THEN J=(J+15)MOD16
440 IF V$=CHR$(30) THEN I=(I+15)MOD16
450 IF V$=CHR$(31) THEN I=(I+1)MOD16
455 IF V$=CHR$(12) THEN 490
456 IF V$=CHR$(27) THEN GOSUB640
457 IF V$<CHR$(11) THEN RUN
460 LOCATE J*3+5,I+1:F=-1:PRINT RIGHT$("0"+HEX$(PEEK(A+I*16+J)),2)" ";
470 LOCATE J*3+5,I+1:RETURN
480 COLOR7:PRINT V$;:COLOR5:RETURN
490 S=0:FORL=0TO15:S=S+PEEK(A+I*16+L):NEXT
495 LOCATE 16*3+5,I+1:PRINT"-"RIGHT$("0"+HEX$(S),2)
496 LOCATE J*3+5,I+1
497 GOTO 350
500 REM write command
510 PRINT:INPUT "Address,ser";V$:GOSUB2000
520 A=VAL("&H"+V$)
530 'A=FNE(A,256)
540 COLOR7:PRINT CHR$(12);
550 COLOR5:PRINT"Adrs ";
560 FOR I=0 TO 15:PRINT"+";HEX$(I);" ";:NEXT:PRINT"SUM"
570 FOR I=0 TO 15:PRINT RIGHT$("000"+HEX$(A+I*16),4)" ":NEXT
580 FOR I=0 TO 15
590 FOR J=0 TO 15
600 LOCATE J*3+5,I+1
610 GOSUB 300
620 NEXT:LOCATE 16*3+5,I+1:PRINT "-";
630 S=0:FORJ=0TO15:S=S+PEEK(A+I*16+J):NEXT:PRINTRIGHT$("0"+HEX$(S),2):NEXT
635 GOSUB 640:PRINT:PRINT:GOTO130
640 LOCATE0,17:PRINT STRING$(57,"•")
650 SS=0:PRINT "SUM: ";:FOR K=0 TO 15:S=0:FOR L=0 TO 15
660 S=(S+PEEK(A+K+L*16))MOD256:NEXT:PRINT RIGHT$("0"+HEX$(S),2)" ";
670 SS=SS+S:NEXT:PRINT "-"RIGHT$("0"+HEX$(SS),2)
680 RETURN
700 REM d cmd
710 GOSUB 800:GOTO130
800 REM dumper
810 PRINT:INPUT "Address,ser";V$:GOSUB2000
820 A=VAL("&H"+V$)
840 COLOR7:PRINT CHR$(12);
850 COLOR5:PRINT"Adrs ";
860 FOR I=0 TO 15:PRINT"+";HEX$(I);" ";:NEXT:PRINT"SUM"
870 FOR I=0 TO 15
880 PRINT RIGHT$("000"+HEX$(A+I*16),4)" ";
890 FOR J=0 TO 15
900 LOCATE J*3+5,I+1
910 COLOR7:PRINT RIGHT$("0"+HEX$(PEEK(A+I*16+J)),2)" ";:COLOR5
920 NEXT:LOCATE 16*3+5,I+1:PRINT "-";
930 S=0:FORJ=0TO15:S=S+PEEK(A+I*16+J):NEXT:PRINTRIGHT$("0"+HEX$(S),2):NEXT
940 GOSUB 640:PRINT:PRINT:RETURN
1000 REM b cmd
1010 GOSUB800:GOTO 580
2000 BEEP1:FOR D=0 TO 20:NEXT:BEEP0:RETURN
9000 PRINT:PRINT:PRINT
9010 PRINT" d - command   dump memory
9020 PRINT" w - command   write to memory
9030 PRINT" b - command   dump memory and write mode
9040 PRINT" h - command   Help operator
9050 PRINT"****  MACHINE WRITER -3  for new I/O check sum  ****
9060 PRINT"        programed by Akie
9070 PRINT" No part of this program shuld be used without permission.
9080 GOTO 130
